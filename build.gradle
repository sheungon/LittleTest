// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        // App version and version code
        app_version_name = '1.0.0'
        app_version_code = 100

        compile_sdk_version = 28
        target_sdk_version = 28

        versions = [
                'kotlin_plugin'      : '1.3.21',
                'kotlin_env'         : 'jdk8',
                // Kotlin coroutines
                // https://github.com/Kotlin/kotlinx.coroutines
                // https://mvnrepository.com/artifact/org.jetbrains.kotlinx/kotlinx-coroutines-android
                'kotlin_coroutines'  : '1.1.1',

                //////////////////////////// Android support lib START ////////////////////////////
                // https://mvnrepository.com/artifact/com.google.android.material/material
                'android_material' : '1.0.0',
                // https://mvnrepository.com/artifact/androidx.cardview/cardview
                'androidx_cardview' :'1.0.0',
                // https://mvnrepository.com/artifact/androidx.constraintlayout/constraintlayout
                'androidx_constraintlayout' :'1.1.3',
                // https://developer.android.com/jetpack/androidx/releases/lifecycle
                'androidx_lifecycle'          : '2.0.0',
                //////////////////////////// Android support lib END ////////////////////////////

                // RecyclerView-FlexibleDivider
                // https://github.com/yqritc/RecyclerView-FlexibleDivider
                'flexiable_divider'  : '1.4.0',

                // Picasso
                // http://square.github.io/picasso/
                'picasso'            : '2.71828',

                // Retrofit 2
                // http://square.github.io/retrofit/
                'retrofit'           : '2.5.0',

                // OkHttp interceptor
                // https://github.com/square/okhttp
                'ok_http_interceptor': '3.13.1',

                // Dagger 2
                // https://github.com/google/dagger
                // See version at,
                // https://mvnrepository.com/artifact/com.google.dagger/dagger
                // https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
                'dagger_2'           : '2.21',

                // DaggerMock
                // https://github.com/fabioCollini/DaggerMock
                'dagger_mock'        : '0.8.4',

                // https://github.com/sheungon/AndroidEasyLog
                'android_ec_log'     : '0.3.1',

                // https://developer.android.com/topic/libraries/testing-support-library/packages.html
                'espresso'           : '3.1.0',

                // Mockito
                // http://site.mockito.org/
                // https://mvnrepository.com/artifact/org.mockito/mockito-android
                'mockito'            : '2.25.0',

                // PowerMock
                // https://github.com/powermock/powermock
                'power_mock'         : '2.0.0',
        ]

        deps = [
                // http://junit.org/junit4/
                'junit': 'junit:junit:4.12'
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.3.2"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_plugin"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        /*
         * For PhotoView, DaggerMock
         * https://github.com/chrisbanes/PhotoView
         */
        maven { url "https://jitpack.io" }

        jcenter()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    /*
    * Fix dependency error
    * https://github.com/JakeWharton/u2020/blob/9870fa4291f5c813ed929aec26c94bee2b92d2b7/build.gradle#L105-L113
    * */
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support'
                        && details.requested.name != 'multidex'
                        && details.requested.name != 'multidex-instrumentation') {
                    details.useVersion "$versions.support_lib"
                }
            }
        }
    }

    /*
    * Fix Dagger 2 swallows build error
    * https://github.com/google/dagger/issues/306#issuecomment-418453683
    * */
    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 5000)
                }
            }
        }
    }
}
